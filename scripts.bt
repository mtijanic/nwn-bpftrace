#define can_be_run_as_shell_script /*
set -e
cpp -E -P -undef -Iinc $0 $* | sudo xargs -0 bpftrace -e
exit 0
# */
#include "base.h"
#include "CExoString.h"


#if INTERACTIVE
// On key press
//    p - prints currently captured stored times
//    c - clears the currently stored times
//    q - exit te program
kprobe:pty_write / arg2 == 1 / {
    $key = *((int8*)arg1);
    if ($key == 112 /* 'p' */) {
        print(@script_exec_times);
    }
    if ($key == 99 /* 'c' */) {
        clear(@script_exec_times);
    }
    if ($key == 113 /* 'q' */) {
        exit();
    }
}
#endif // INTERACTIVE


uprobe:NWSERVER:_ZN15CVirtualMachine9RunScriptEP10CExoStringjii {
    @recursion++;
    @start_time[@recursion] = nsecs;
    @name[@recursion] = CEXOSTR_TO_STR(arg1);
}

uretprobe:NWSERVER:_ZN15CVirtualMachine9RunScriptEP10CExoStringjii / @start_time[@recursion] / {
    $exectime = nsecs - @start_time[@recursion];
    $name = @name[@recursion];

    @script_exec_times[$name] = stats($exectime);
    INFO("%s.ncs took %lld ns\n", $name, $exectime)

    @recursion--;
}

END {
    clear(@start_time);
    clear(@name);
    delete(@recursion);
}

